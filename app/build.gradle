plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'org.jetbrains.kotlin.plugin.compose'
    id 'kotlin-kapt'
    id 'dagger.hilt.android.plugin'
    id 'kotlin-parcelize'
    id 'com.mikepenz.aboutlibraries.plugin'
}

apply from: './gradleScript/dependencies.gradle'

android {
    def file = rootProject.file("local.properties")
    def keystorePropertiesFile = rootProject.file("keystore.properties")
    def keystoreProperties = new Properties()
    if (keystorePropertiesFile.exists()) {
        keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
    }

    namespace "wwon.seokk.abandonedpets"
    compileSdk 35

    defaultConfig {
        applicationId "wwon.seokk.abandonedpets"
        minSdk 26
        targetSdk 35
        versionCode 3
        versionName "3.0.0"
        vectorDrawables.useSupportLibrary = true
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        buildConfigField 'String', 'BASE_URL', "\"http://apis.data.go.kr/1543061/abandonmentPublicService_v2/\""
        if (file.exists()) {
            def localProperties = new Properties()
            localProperties.load(new FileInputStream(file))
            buildConfigField 'String', 'API_KEY', "\"" + localProperties['api.key'] + "\""
        } else {
            buildConfigField 'String', 'API_KEY', "\"" + System.getenv("API_KEY") + "\""
        }
    }

    signingConfigs {
        release {
            if (keystorePropertiesFile.exists()) {
                keyAlias keystoreProperties['keyAlias']
                keyPassword keystoreProperties['keyPassword']
                storeFile rootProject.file(keystoreProperties['storeFile'])
                storePassword keystoreProperties['storePassword']
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
    kotlinOptions {
        jvmTarget = '11'
    }
    buildFeatures {
        compose true
        buildConfig true
    }
    testOptions {
        unitTests {
            returnDefaultValues = true
            includeAndroidResources = true
        }
    }

}
